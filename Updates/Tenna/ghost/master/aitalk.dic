//---------------------------AI Talk--------------------------
//Alright, this is it! The bread and butter of your ghost. This is where all their random conversations are stored. Of all the .dic files, this is the one you will probably be adding stuff to and updating the most as new dialogue comes to your mind. It is relatively simple in that there aren't many functions or real coding involved with this file, but there are a few cool tricks you can do within randomtalk that can make dialogue with interesting effects, so I'll point them out as we go along.

//I'm going to leave a lot of examples of different types of dialogue in here that you can study if the possibilities they present intrigue you. Study them carefully!


//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//If you see a big block of commented text you want to uncomment, highlight the text and then hit ctrl-shift-k in Notepad++ and that should uncomment it. Mostly this is intended for advanced users.

/*
This file has...

AiTalk
RandomTalk
any chains you set up
HourCheck
OnMinuteChange
OnSecondChange
OffscreenTalk
EndOffscreenTalk
OnSurfaceRestore
OnKeyPress
*/


//--OnAiTalk
//Leave this alone. This is mostly setting up whether or not dialogue is in a chain before it displays.

OnAiTalk
 {
	if RAND(100) < communicateratio
 	{
 		StartCommunicate
	}
 	else
	{
			if CHAIN.IDName == "" {
 				lastTalk = RandomTalk
			}
			else {
				lastTalk = ChainTalk
			}
			lastTalk
	}
 }
 
 
 //*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//--Mikiretalk, or the Off-Screen Boundary Check--

//This has been a feature in the template for a very long time, and while it's enjoyable enough for some, for others it can be rather irritating or goes against what their ghost is. So to that end, I'm making this an opt-in feature. Basically, this flag checks to see if the ghost is off the screen and haves them say something to the user to get them to move them back on screen. If you want to enable this, it'll take a bit of fussing around. You're going to want to add the mikireflag check into OnAiTalk above. You can see how so below. Then you'll have to follow up on the flag check later down at OnSecondChange. Keep an eye out.

 
//
// OnAiTalk
 // {
	// if RAND(100) < communicateratio
 	// {
 		// StartCommunicate
	// }
 	// else
	// {
 		// if mikireflag == 0
		// {
			// if CHAIN.IDName == "" {
 				// lastTalk = RandomTalk
			// }
			// else {
				// lastTalk = ChainTalk
			// }
			// lastTalk
		// }
 		// else
		// { 
		// MikireTalk
 		// }
	// }
 // }
//***********************************************************************


//--RandomTalk 
 //Alright, here we go! This is where ALL your ghost's random dialogue will go! These are the conversations that they'll have with you or each other that pop up depending on the talk rate defined in menu.dic (or if you ignored that, set in bootend.dic in the first boot, which would be every five minutes).
 
 
//*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//You CAN link to other pools of dialogue in RandomTalk using envelopes/functions, such as %(teatalk) to pull from a pool of dialogue about tea. Then you'd just add that line in among the other dialogue in here with %(teatalk).

//You can't do things like put OnSystemBoot or something in RandomTalk. You could use \![raise] to pull something though, although I'd be careful about it. More on these kind of things and conditionals in general in the coding page on the walkthrough.

//If you split up RandomTalk with if/else checks, then you can redirect in an If to another function, like... if chargermode == "on" { ChargerChatting } else {normal random talk dialogue}. This would redirect the random dialogue to ChargerChatting instead. You can't just throw a function into RandomTalk directly though, like "dialogue 1" "dialogue 2" OnFunctionname "dialogue 3", that kind of thing. You'll have to split it off into its own if or point to it with an envelope.

//You can do multiple ifs in RandomTalk if required... like an if for if a character is asleep, or if a third character is present, or if they're having a nervous breakdown, or if they're sulking. It depends! You can add a bunch of checks leading to specialized pools if you want. Just make sure you leave some normal dialogue in the last else for RandomTalk (unless you want to redirect that somewhere else with an envelope too... although that seems a bit redundant, honestly.)
//***********************************************************************
dramaname
{
	"Fernando"
	"Franziska"
	"Leonardo"
	"Maximillian"
	"Ella"
	"Priscilla"
	"Jim"
	"Sussiella"
	"Noellando"
	"Berdlithan"
	"Veronica"
	"Stanly"
	"Tennatenno"
	"Spamtonio"
	"Krissthian"
	"Asgorro"
	"Sanson"
	"Tutoriella"
	"Ralsello"
	"John"
	"Leila"
	"Stanson"
	"Anatolio"
	"Mark"
	"Gina"
	"Amanda"
	"Markus"
	"Undina"
	"Alphanny"
	"Bonny"
	"Matteo"
	"Anne"
	"Sasha"
	"Nikita"
	"Mr.Man"
	"Telly Novellio"
	"Ms.Millan"
	"Mr.Mac"
	"Ms.Jillian"
	"Mr.Cool"
	"Ms.Beauty"
}
ghostname
{
	"Big Claw"
	"Prince Boo"
	"Spiderhands"
	"Demon Clown"
	"Vendimachinego"
	"Scarf-face"
	"Evil Lizard"
	"Hook-car-hand-door"
	"Killer Bee"
	"Shadow Mantaray"
	"Ghostygoo"
	"Annet VT"
	"Blood Boot"
	"Mothman"
	"Skelebone"
	"Zombieron"
	"Jerry"
	"Lady D. Evil"
	"Dr. Scare"
}

//nonoverlap_pool should make it so all your dialogue has an equal probability of being called, and it will do each line of dialogue before repeating them. "Why" is kind of technical and a bit outside the scope of this template, you can look into why elsewhere if you want.

RandomTalk : nonoverlap_pool
{
	//watch stuff with tenna
	if tvtogether == 1
	{
		//videos
		if watchingvideos == 1
		{
			"\0\s[26]\i[7]*(You hear him mutter something.)\w8\w2\s[26]\n\n[half]\f[sub,true]*I find TV more appealing...\f[sub,false]\i[7]\e"
			"\0\s[26]*Looks like we'll have some copyright problems with this one, \w2folks!\i[7]\e"
			"\0\s[26]*You can't just put THIS on the screen!!!\i[7]\e"
			"\0\s[26]*Mike, \w2are you taking notes on the editing?\i[7]\e"
			"\0\s[26]*%(username), \w2is this what you choose to watch?\i[7]\e"
			"\0\s[28]*What was \w2THAT?!?\i[7]\e"
			"\0\s[26]*H\w3u\w3U\w3u\w3u\w3U\w3U\w3u\w3u\w3u\w3U\w3u\w3U\w3u\w3U\w3U\w3h\w3h\w3H\w3H\w3H\w3H\w3!?\i[7]\e"
			"\0\s[26]*Is this how families feel when they gather in front of a television?\i[7]\e"
			"\0\s[26]*%(username), \w2are you sure this is family friendly?\i[7]\e"
			"\0\s[26]*HUH, \w2%(username),\s[28]\w2did you see that?? \w8\w2Mike, \w2did you?? \w8\w2\s[25]\i[1]Rewind!!\s[25]\i[7]\e"
			"\0\s[26]*This would be more thrilling on a big screen. \w8\w2\s[25]With a big nose!!\i[7]\e"
			"\0\s[28]*Where's the laugh track?? \w8\w2How do you tell when you should laugh???\i[7]\e"
			"\0\s[28]*I can do this too!!\i[7]\e"
			"\0\s[28]*hOLY HECK!!!!\i[7]\e"
			"\0\s[26]*Mike, \w2are you taping this??\i[7]\e"
			"\0\s[26]*%(username), \w2\s[28]look at \w2\i[1]THAT!!!\s[28]\i[7]\e"
			"\0\s[25]*Now \w3THAT\w3 was clever!!\i[7]\e"
			"\0\s[26]*Now \w2\s[25]\i[1]THIS is \s[25]entertainment!!\i[7]\e"
			"\0\s[26]*Is this what TV is like nowadays? \w8\w2No wonder this generation is \i[1]wack!\s[26]\i[7]\e"
			"\0\s[26]*Is this what TV is like nowadays? \w8\w2No wonder this generation is \i[1]doomed!\s[26]\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he's comfortable.)\w8\w2\s[26]\n\n[half]*My legs might be too long, \w2but it's my burden to bear!\w8\w2\n*Mike, add [bear sfx]!!\i[7]\e"
			"\0\s[26]\i[7]*(He seems engrossed in what's going on.)\e"
			"\0\s[26]\i[7]*(He's paying attention.)\e"
			"\0\s[26]\i[7]*(He's listening intently.)\e"
			"\0\s[26]\i[7]*(He's watching with his nose.)\e"
			"\0\s[26]\i[7]*(His antennas are listening.)\e"
		}
		//movies
		elseif watchingmovies == 1
		{
			"\0\s[26]*Ah, \w2truly a classic!!\i[7]\e"
			"\0\s[28]*THIS twist just demans a commercial break!!!\i[7]\e"
			"\0\s[25]*Mike, \w2put this one in the schedule for later!\i[7]\e"
			"\0\s[26]*%(username), \w2is this kind of movies you tune in for?\i[7]\e"
			"\0\s[28]*What was \w2THAT?!?\i[7]\e"
			"\0\s[26]*What a quirky cast of characters!\i[7]\e"
			"\0\s[26]*I missed some good ol' CINEMA!!\i[7]\e"
			"\0\s[28]\i[1]*\w4C\w4I\w4N\w4E\w4M\w4A\w4!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[28]\i[1]is that \s[28]you?? \w8\w2You didn't tell me you were a movie star as well!!\i[7]\e"
			"\0\s[26]*H\w3u\w3U\w3u\w3u\w3U\w3U\w3u\w3u\w3u\w3U\w3u\w3U\w3u\w3U\w3U\w3h\w3h\w3H\w3H\w3H\w3H\w3!?\i[7]\e"
			"\0\s[26]*Now that's a movie for the whole family!!\i[7]\e"
			"\0\s[26]*%(username), \w2are you equipped with snacks?\i[7]\e"
			"\0\s[26]*HUH, \w2%(username),\s[28]\w2did you see that?? \w8\w2Mike, \w2did you?? \w8\w2\s[25]\i[1]Rewind!!\s[25]\i[7]\e"
			"\0\s[26]*I sure haven't seen THIS coming, \w2folks!!\i[7]\e"
			"\0\s[26]*Wow, \w2those practical effects are so convincing!!\i[7]\e"
			"\0\s[28]*hOLY HECK!!!!\i[7]\e"
			"\0\s[26]*Mike, \w2are you taping this??\i[7]\e"
			"\0\s[26]*%(username), \w2\s[28]look at \w2\i[1]THAT!!!\s[28]\i[7]\e"
			"\0\s[26]*This would be better with a giant monster! \w8\w2\i[1]Or even better, \w2\s[25]\i[1]a giant\s[25] TV!!\i[7]\e"
			"\0\s[26]*Now \w2\s[25]\i[1]THIS is \s[25]entertainment!!\i[7]\e"
			"\0\s[26]*This one is a doozy!!\e"
			"\0\s[26]\i[7]*(He seems engrossed in what's going on.)\e"
			"\0\s[26]\i[7]*(He's paying attention.)\e"
			"\0\s[26]\i[7]*(He's listening intently.)\e"
			"\0\s[26]\i[7]*(He's watching with his nose.)\e"
			"\0\s[26]\i[7]*(His antennas are listening.)\e"
			"\0\s[26]\i[7]*(He's trying to guess the ending.)\e"
		}
		//music
		elseif watchingmusic == 1
		{
			"\0\s[26]\i[7]*(He's singing along.)\e"
			"\0\s[26]*Looks like we'll have some copyright problems with this one, \w2folks!\i[7]\e"
			"\0\s[26]*You can't just play THIS on this channel!!!\i[7]\e"
			"\0\s[25]\i[1]*This can \s[25]be my new jingle!\i[7]\e"
			"\0\s[26]*%(username), \w2is this your choice of music? \w8\w2EVERYTHING MAKES SENSE NOW!!\i[7]\e"
			"\0\s[26]*What a t\w3u\w3U\w3u\w3u\w3U\w3u\w3U\w3u\w3U\w3U\w3U\w3n\w3e\w3!!\i[7]\e"
			"\0\s[26]*Not enough pep in this one for me!\i[7]\e"
			"\0\s[26]*%(username), \w2are you sure this is family friendly?\i[7]\e"
			"\0\s[26]*Now \w2\s[28]THIS\w2 is a \w2BOP!\i[7]\e"
			"\0\s[28]\i[1]*ENCORE! \w8\w4\s[28]\i[1]ENCORE!\i[7]\e"
			"\0\s[28]*I can sing this better!!\i[7]\e"
			"\0\s[28]*Ear worm incoming, \w2folks!!\i[7]\e"
			"\0\s[26]*Mike, \w2stop dancing!!\i[7]\e"
			"\0\s[26]*Now \w2\s[25]\i[1]THIS is \w2\s[25]MUSIC!!\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he wants to use his microphone.)\w8\w2\s[25]\n\n[half]*My microphone is for professional yapping, \w2not frivolous singing!!\i[7]\e"
			"\0\s[26]\i[7]*(He is censoring lyrics for the broadcast.)\e"
			"\0\s[26]\i[7]*(He's humming.)\e"
			"\0\s[26]\i[7]*(He's listening intently.)\e"
			"\0\s[26]\i[7]*(He's trying to tune this one out.)\e"
			"\0\s[26]\i[7]*(His antennas are tingling.)\e"
			"\0\s[26]\i[7]*(He still thinks about the previous song.)\e"
			"\0\s[26]\i[7]*(He's judging your playlist.)\e"
		}
		//gaming
		elseif watchinggaems == 1
		{
			"\0\s[26]\i[7]*(You hear him mutter something.)\w8\w2\s[26]\n\n[half]\f[sub,true]*The budget on this...\f[sub,false]\i[7]\e"
			"\0\s[28]*You're doing wonderful!!\i[7]\e"
			"\0\s[28]\i[1]*Our \s[28]contestant makes a brilliant move!!!\i[7]\e"
			"\0\s[26]*Folks, \w2\s[28]look at this [skill]!!!\i[7]\e"
			"\0\s[26]*Now \w2\s[28]THIS \w2is \w2\i[1]ACTION!!!\i[7]\e"
			"\0\s[26]*Mike, \w2look at \w2\s[28]THIS!!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[28]where are the points??\i[7]\e"
			"\0\s[28]*What is \w2THAT?!?\i[7]\e"
			"\0\s[28]\i[1]*Look \s[28]over there!!!\i[7]\e"
			"\0\s[26]*What a thrilling game show!!\i[7]\e"
			"\0\s[26]*%(username), \w2I hope you try as much on my game show too!!\i[7]\e"
			"\0\s[26]*Missed that!!\i[7]\e"
			"\0\s[25]*This can make a good setting for my next board!!\i[7]\e"
			"\0\s[25]*Mike, \w2\i[1]cut this\s[25] part out!!\i[7]\e"
			"\0\s[28]*PIXELS!!!!\i[7]\e"
			"\0\s[28]*This looks FUNKY!!\i[7]\e"
			"\0\s[28]*Mike, \w2are you writing these ideas down??\i[7]\e"
			"\0\s[26]*Mike, \w2take photos!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[25]\i[1]remember to \s[25]rest your eyes by looking at some \i[1]TV!!\i[7]\e"
			"\0\s[26]*When do the quizes start???\i[7]\e"
			"\0\s[26]\i[7]*(You ask him for tips.)\w8\w2\s[25]\n\n[half]*Dodge if you don't want to get hit!!!\i[7]\e"
			"\0\s[26]\i[7]*(He is watching the game.)\e"
			"\0\s[26]\i[7]*(He's paying attention.)\e"
			"\0\s[26]\i[7]*(His lack of eyes is watching closely.)\e"
			"\0\s[26]\i[7]*(He's watching with his nose.)\e"
			"\0\s[26]\i[7]*(He's engrossed in the action.)\e"
			"\0\s[26]\i[7]*(He's thinking about his own game show.)\e"
		}
		//art
		elseif watchingart == 1
		{
			"\0\s[26]\i[7]*(You hear him mutter something.)\w8\w2\s[26]\n\n[half]\f[sub,true]*Nose can be bigger...\f[sub,false]\i[7]\e"
			"\0\s[28]*You're doing wonderful!!\i[7]\e"
			"\0\s[26]*Now \w2\s[28]THIS \w2belongs on the big screen!!!\i[7]\e"
			"\0\s[26]*Mike, \w2look at \w2\s[28]THIS!!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[28]you got some talent here! \w8\w2\s[25]\i[1]And trust \s[25]me, \w2as a showman I can smell a talent a mile away!! \w8\w2\i[1]Just look\s[25] at my nose!!!\i[7]\e"
			"\0\s[28]*What is \w2THAT?!?\i[7]\e"
			"\0\s[26]*This is getting GLOOBY!!\i[7]\e"
			"\0\s[26]*Feels like watching Glob Noss!\i[7]\e"
			"\0\s[26]*%(username), \w2careful with your pen there, \w2\s[25]\i[1]we don't \s[25]want to get demonatized now!\i[7]\e"
			"\0\s[26]*You erased THAT??\i[7]\e"
			"\0\s[25]*Maybe you can draw a handsome TV show host next!!\i[7]\e"
			"\0\s[28]*This would look better with a neon yellow tie!!\i[7]\e"
			"\0\s[28]*PIXELS!!!!\i[7]\e"
			"\0\s[28]*This looks FUNKY!!\i[7]\e"
			"\0\s[26]*Mike, \w2take photos!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[25]\i[1]remember to \s[25]rest your eyes by looking at some \i[1]TV!!\i[7]\e"
			"\0\s[25]*Now \w3THAT'S\w3 color!!\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he's bored.)\w8\w2\s[28]\n\n[half]*This is more action that I've gotten in years!! \w8\w1\i[1]I love \s[28]pixels!!!\i[7]\e"
			"\0\s[26]\i[7]*(He is watching the canvas.)\e"
			"\0\s[26]\i[7]*(He's paying attention.)\e"
			"\0\s[26]\i[7]*(His lack of eyes is watching closely.)\e"
			"\0\s[26]\i[7]*(He's watching with his nose.)\e"
			"\0\s[26]\i[7]*(He's enjoying the art.)\e"
			"\0\s[26]\i[7]*(He's a bit jealous.)\e"
		}
		//writing
		elseif watchingwriting == 1
		{
			"\0\s[26]*You're doing GREAT!!\i[7]\e"
			"\0\s[26]\i[1]*MORE WORDS!!!!\i[7]\e"
			"\0\s[26]\i[1]*NEEDS!! \w8\w1\i[1]M\w2O\w2R\w2E\w2!\w2!\w2! \w8\w1\i[1]W\w3O\w3R\w3D\w3S\w3!\w3!\w3!\w3!\w3!\i[7]\e"
			"\0\s[26]*THIS is getting \i[1]SPICY!!!\i[7]\e"
			"\0\s[26]*That's a lot of words!!!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[25]you need another \w3TWIST!!!\i[7]\e"
			"\0\s[26]*Now \w2\i[1]SOMEONE\w2 \s[26]has swallowed a dictionary!!\i[7]\e"
			"\0\s[26]*%(username), \w2how big is your budget for THIS many letters?!\i[7]\e"
			"\0\s[25]*You should write things on my cards later!\i[7]\e"
			"\0\s[26]*%(username), \w2your typewriter skills are incredible!!\i[7]\e"
			"\0\s[26]*You erased THAT??\i[7]\e"
			"\0\s[26]*I haven't seen this one coming, \w2folks!!\i[7]\e"
			"\0\s[25]*You should add more pizzazz!!\i[7]\e"
			"\0\s[28]*MORE EXCLAMATION POINTS!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!\i[7]\e"
			"\0\s[26]*This is a DOOZY!!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[25]\i[1]remember to \s[25]rest your eyes by looking at some \i[1]TV!!\i[7]\e"
			"\0\s[25]*Now \w3THAT'S\w3 an unappreciated word!!\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he's bored.)\w8\w2\s[28]\n\n[half]*Without words there would be no television, \w2and without letters there would be no \i[1]TV!!!\i[7]\e"
			"\0\s[26]\i[7]*(He is watching the page.)\e"
			"\0\s[26]\i[7]*(He's reading incredibly slowly.)\e"
			"\0\s[26]\i[7]*(His lack of eyes is watching closely.)\e"
			"\0\s[26]\i[7]*(He's questioning your plot choices.)\e"
			"\0\s[26]\i[7]*(He's thinking about adapting this for TV.)\e"
			"\0\s[26]\i[7]*(He's considering borrowing some of your letters.)\e"
		}
		//homework
		elseif watchinghomework == 1
		{
			"\0\s[26]*You're making great progress!!\i[7]\e"
			"\0\s[26]*Just a bit more, \w2and you can go back into welcoming embracing arms of your TV!!!!\i[7]\e"
			"\0\s[26]*I bet you learned \w2THAT\w2 on \w2TV!!!\i[7]\e"
			"\0\s[26]*%(username), \w2eyes on the prize!\i[7]\e"
			"\0\s[28]*This is RIDICULOUS!!!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[28]do you really need to know this? \w8\w1\s[26]\i[1]Cooky!!!\i[7]\e"
			"\0\s[26]*Now \w2\i[1]SOMEONE\w2 \s[26]is a smarty pants!!\i[7]\e"
			"\0\s[28]*I know this!![lie]\i[7]\e"
			"\0\s[26]*Mike, \w2\s[28]do you know this???\i[7]\e"
			"\0\s[26]*%(username), \w2you got this!! \w8\w2\s[28]The audience is cheering for you!\i[7]\e"
			"\0\s[26]*Ah yes, \w2this!\i[7]\e"
			"\0\s[26]*This will be on our quiz later!!\i[7]\e"
			"\0\s[26]*What a QUIZ!!\i[7]\e"
			"\0\s[26]*OK %(username) this is real hard but you got this holy heck what the heck is this.\i[7]\e"
			"\0\s[26]*This is a DOOZY!!\i[7]\e"
			"\0\s[26]*%(username), \w2\s[25]\i[1]remember to \s[25]rest your eyes by looking at some \i[1]TV!!\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he's bored.)\w8\w2\s[25]\n\n[half]*More like PUZZLED!\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he can help.)\w8\w2\s[25]\n\n[half]*With the next question, \w2surely!!!\s[28]\i[7]\e"
			"\0\s[26]\i[7]*(He is watching the page.)\e"
			"\0\s[26]\i[7]*(He's reading incredibly slowly.)\e"
			"\0\s[26]\i[7]*(He is unsure of what's going on.)\e"
			"\0\s[26]\i[7]*(He's going through his educational programing librarby.)\e"
			"\0\s[26]\i[7]*(Did you see this on TV?)\e"
		}
		//stuff
		else
		{
			"\0\s[26]\i[7]*(You hear him mutter something.)\w8\w2\s[26]\n\n[half]\f[sub,true]*I find TV more appealing...\f[sub,false]\i[7]\e"
			"\0\s[26]*You can't just put THIS on the screen!!!\i[7]\e"
			"\0\s[26]*Mike, \w2turn on some lights here!!\i[7]\e"
			"\0\s[26]*%(username), \w2is this what you're usually doing? \w8\w2\s[28]You can spend this time watching TV!!\i[7]\e"
			"\0\s[28]*What is \w2THAT?!?\i[7]\e"
			"\0\s[26]*H\w3u\w3U\w3u\w3u\w3U\w3U\w3u\w3u\w3u\w3U\w3u\w3U\w3u\w3U\w3U\w3h\w3h\w3H\w3H\w3H\w3H\w3!?\i[7]\e"
			"\0\s[25]*I like this!!!\i[7]\e"
			"\0\s[26]*%(username), \w2are you sure you want this broadcasted to our audiences?\i[7]\e"
			"\0\s[26]*HUH, \w2%(username),\s[28]\w2is that normal??\i[7]\e"
			"\0\s[26]*This would be more thrilling on a big screen. \w8\w2\s[25]With a big nose!!\i[7]\e"
			"\0\s[28]*This belongs on the screen!!\i[7]\e"
			"\0\s[28]*hOLY HECK!!!!\i[7]\e"
			"\0\s[26]*Mike, \w2are you taping this??\i[7]\e"
			"\0\s[26]*%(username), \w2do that again!!\i[7]\e"
			"\0\s[26]\i[7]*(You ask him if he's comfortable.)\w8\w2\s[26]\n\n[half]*My legs might be too long, \w2but it's my burden to bear!\w8\w2\n*Mike, add [bear sfx]!!\i[7]\e"
			"\0\s[26]\i[7]*(He's looking around.)\e"
			"\0\s[26]\i[7]*(He's paying attention.)\e"
			"\0\s[26]\i[7]*(He's listening intently.)\e"
			"\0\s[26]\i[7]*(He's watching with his nose.)\e"
			"\0\s[26]\i[7]*(His antennas are listening.)\e"
		}
	}
	//TV box 
	elseif notatv == 0
	{
		"*(...)\e"
		"*(...)\e"
		"*(...)\e"
		"*(...)\e"
		"*(...)\e"
		"*(...)\e"
		"*(It's silent.)\e"
		"*(It's a TV.)\e"
		"*(Smells like static.)\e"
		"*(A lonely TV box.)\e"
		"*(It's a square wtih antennas.)\e"
		"*(It's a box for watching stuff.)\e"
		"*(It's a TV. \w8\w2You can turn it on.)\e"
		"*(It's a TV. \w8\w2It's old, \w2but seems to be in a working condition.)\e"
	}
	//watching what Tenna showing
	elseif tvwatch == 1
	{
		//cooking channel
		if watchingch1 == 1
		{
			"\s[30]* \w3.\w3.\w3.\w3now add some eggs! \w8\w1Don't forget to remove the yolks and the whites!!\e"
			"\s[30]* \w3.\w3.\w3.\w3There is no such thing as 'too much sugar'!\e"
			"\s[30]* \w3.\w3.\w3.\w3WHERE IS THE LAMB SAUCE?!???\e"
			"\s[30]* \w3.\w3.\w3.\w3Remember to punch your tomatoes as if they're your enemies!\e"
			"\s[30]* \w3.\w3.\w3.\w3Turn up the heat!!\e"
			"\s[30]* \w3.\w3.\w3.\w3Turn up the heat!! \w8\w1Hotter!! \w8\w1HOTTER!!!\e"
			"\s[30]* \w3.\w3.\w3.\w3Remember your trusty pan! \w8\w1Unlike a man, \w2it will never leave you!\e"
			"\s[30]* \w3.\w3.\w3.\w3Fry while the oil is hot!\e"
			"\s[30]* \w3.\w3.\w3.\w3Now, onto the salt\w2 - get out your biggest spoon!\e"
			"\s[30]* \w3.\w3.\w3.\w3Make sure to taste your food, \w3but no spitting back!\e"
			"\s[30]* \w3.\w3.\w3.\w3Just like mama used to make!\e"
			"\s[30]* \w3.\w3.\w3.\w3Now, \w2add four more gramms of paprika!\e"
			"\s[30]* \w3.\w3.\w3.\w3Whoops, \w2this was too much... \w8\w2Ignore this step!\e"
			"\s[30]* \w3.\w3.\w3.\w3Only three more minutes on the fire!\e"
			"\s[30]* \w3.\w3.\w3.\w3Stir faster! \w8\w2Until your spoon is just a stick!\e"
			"\s[30]* \w3.\w3.\w3.\w3No sour faces in \w2MY\w2 kitchen!\e"
			"\s[30]* \w3.\w3.\w3.\w3Now, add another lemon!\e"
			"\s[30]* \w3.\w3.\w3.\w3Toss in more garlic, \w2then gently stir!\e"
			"\s[30]* \w3.\w3.\w3.\w3Deeelicious! \w8\w2Can you say 'MMMM' for me? \w8\w1 MMMMMMMMMMMHHHMHMHMMMMMMMMMMHHHMMMMMMMMMMMMHMHMHMMMMMMMMMMMMM...\e"
			"\s[30]* \w3.\w3.\w3.\w3get the banana...\e"
			"\s[30]* \w3.\w3.\w3.\w3Only four more steps, \w2and our dish is done!\e"
			"\s[30]* \w3.\w3.\w3.\w3You can add some apple juice if you're feeling \f[italic,true]fruity~\f[italic,false]\e"
			"\s[30]* \w3.\w3.\w3.\w3Now, \w2let's not get too adventurous with the watermelon...\e"
			"\s[30]* \w3.\w3.\w3.\w3Don't let the milk run away! \w8\w2It's wanted in three states!\e"
			"\s[30]* \w3.\w3.\w3.\w3just slap some meat on there, \w2should be fine...\e"
			"\s[30]* \w3.\w3.\w3.\w3we can't waste eggs in THIS economy...\e"
			"\s[30]* \w3.\w3.\w3.\w3fruit salad, \w2yummy yummy...\e"
			"\s[30]* \w3.\w3.\w3.\w3almost as delicious as TV Dinner!\e"
			"\s[30]* \w3.\w3.\w3.\w3Looks like it started to boil! \w2Add more rice!\e"
			"\s[30]* \w3.\w3.\w3.\w3Tomato thomato, \w2potato photato...\e"
			"\s[30]* \w3.\w3.\w3.\w3Wait, \w2was it oil or olive?.. \w8\w2Probably not important!\e"
			"\s[30]* \w3.\w3.\w3.\w3Now that's a pie worthy of a face!\e"
			"\s[30]*(The chef is kneading the dough.)\e"
			"\s[30]*(It's footage of best noodles to eat raw.)\e"
			"\s[30]*(The chef is talking about snails.)\e"
			"\s[30]*(It's a dish that requires 28 ingridients to make.)\e"
		}
		//drama channel
		elseif watchingch2 == 1
		//%(dramaname)
		{
			"\s[31]* \w3.\w3.\w3.\w3%(dramaname), \w4I beg you, \w2forgive me!\n\n[half]\w8\w1*Never!..\e"
			"\s[31]* \w3.\w3.\w3.\w3I am in love with you! \w8\w2Please! \w8\w2Give me another chance!!..\e"
			"\s[31]* \w3.\w3.\w3.\w3Oh, \w2%(dramaname)!\w8\w1\n\n[half]*Ah,\w2 %(dramaname)!..\e"
			"\s[31]* \w3.\w3.\w3.\w3I'll always remember you, \w2%(dramaname)...\e"
			"\s[31]* \w3.\w3.\w3.\w3I'm sorry, \w2%(dramaname), \w2but I'm in love with %(dramaname)!\w8\w1\n\n[half]*How could you!..\w2\e"
			"\s[31]* \w3.\w3.\w3.\w3Oh %(dramaname), \w2how I wish to see you...\e"
			"\s[31]* \w3.\w3.\w3.\w3Oh %(dramaname), \w2how I miss you so...\e"
			"\s[31]* \w3.\w3.\w3.\w3I always loved you, \w2%(dramaname)!\w8\w1\n\n[half]*It's too late, \w2%(dramaname)...\e"
			"\s[31]* \w3.\w3.\w3.\w3He cheated on you!\w8\w1\n\n[half]*With who?!\w8\w1\n\n[half]*%(dramaname)!!\w8\w1\n\n[half]*NOOOOOOOOOOO!\e"
			"\s[31]* \w3.\w3.\w3.\w3Did you hear... %(dramaname) is unfaithful!!\e"
			"\s[31]* \w3.\w3.\w3.\w3Oh, \w2I thought we had something special! \w8\w3How could you?!\e"
			"\s[31]* \w3.\w3.\w3.\w3Our love... \w3is DOOMED!!\e"
			"\s[31]* \w3.\w3.\w3.\w3A letter?! \w8\w2From %(dramaname)?! \w8\w2Oh my...\e"
			"\s[31]* \w3.\w3.\w3.\w3Kiss me...\w8\w1\n\n[half]*Oh, \w2%(dramaname)!..\e"
			"\s[31]* \w3.\w3.\w3.\w3*, \w2my soul yearns for you and you alone...\e"
			"\s[31]* \w3.\w3.\w3.\w3I've seen you... \w8\w2with HIM!\w8\w1\n\n[half]*I can explain!..\e"
			"\s[31]* \w3.\w3.\w3.\w3This doesn't mean anything!\w8\w1\n\n[half]*To me, \w2it means \w2EVERYTHING!..\e"
			"\s[31]* \w3.\w3.\w3.\w3I... \w2am pregnant.\w8\w1\n\n[half]*GASP!...\e"
			"\s[31]* \w3.\w3.\w3.\w3After all these years, \w2I still can't forget about %(dramaname)...\e"
			"\s[31]* \w3.\w3.\w3.\w3Have you heard about %(dramaname) and %(dramaname)?..\w8\w1\n\n[half]*Them? \w8\w1I would never have guessed!..\e"
			"\s[31]* \w3.\w3.\w3.\w3you... came back for me?...\w8\w1\n\n[half]*I told you I would.\e"
			"\s[31]* \w3.\w3.\w3.\w3I promise, \w2my love, \w2I'll come back to you...\e"
			"\s[31]* \w3.\w3.\w3.\w3I'm sorry, \w2%(dramaname), \w2but I'm in love with someone else...\e"
			"\s[31]* \w3.\w3.\w3.\w3You brought me nothing but pain! \w8\w2Begone! \w8\w2And never return!\e"
			"\s[31]* \w3.\w3.\w3.\w3I told you how I feel...\w8\w1\n\n[half]*%(dramaname), \w2please...\e"
			"\s[31]* \w3.\w3.\w3.\w3I know you have feelings for %(dramaname), \w2but... \w8\w1Choose me.\w8\w1\n\n[half]Oh, \w2%(dramaname)...\e"
			"\s[31]* \w3.\w3.\w3.\w3I will confess to %(dramaname).\w8\w1\n\n[half]*%(dramaname), \w2you're crazy!\e"
			"\s[31]* \w3.\w3.\w3.\w3Oh, \w2sweet sweet %(dramaname)...\w8\w1I never loved you...\e"
			"\s[31]* \w3.\w3.\w3.\w3don't forget, \w2I'll always be here for you.\w8\w1\n\n[half]*%(dramaname)...\w8\w1\n\n[half]*I promise...\e"
			"\s[31]* \w3.\w3.\w3.\w3I never felt this way before...w8\w1\n\n[half]*Oh, \w1%(dramaname)...\e"
			"\s[31]* \w3.\w3.\w3.\w3We're over!\w8\w1\n\n[half]*No, \w2please!\w8\w1\n\n[half]*I don't want to hear it!\w8\w1\n\n[half]*%(dramaname)...\e"
			"\s[31]* \w3.\w3.\w3.\w3I never told you, \w2but... %(dramaname) is my ex.\w8\w1\n\n[half]*What?!...\e"
			"\s[31]* \w3.\w3.\w3.\w3%(dramaname), \w2marry me!\w8\w1\n\n[half]*Oh, \w2%(dramaname)... \w8\w1Yes!!\e"
			"\s[31]*(It's a very passionate kiss. \w8\w2The lips are almost touching.)\e"
			"\s[31]*(Two people are holding hands. \w8\w2Scandalous.)\e"
			"\s[31]*(A lot of incoherent crying.)\e"
			"\s[31]*(People are singing about feelings.)\e"
			"\s[31]*(People are fighting. \w8\w2All verbal fighting is censored, \w2but physical fighting is left intact.)\e"
			"\s[31]*(A very touching love song is playing.)\e"
			"\s[31]*(Two people are looking into each other's eyes very intently.)\e"
		}
		//horror channel
		elseif watchingch3 == 1
		//%(ghostname)
		{
			"\s[32]* \w3.\w3.\w3.\w3Is this a dead body??\w8\w1\n\n[half]* ...I think this is just a twig.\w8\w1\n\n[half]*Oh.\e"
			"\s[32]* \w3.\w3.\w3.\w3What is that??\w8\w1\n\n[half]*The ghost of %(ghostname)!!\e"
			"\s[32]* \w3.\w3.\w3.\w3We should split up.\w8\w1\n\n[half]*A wonderful idea!\e"
			"\s[32]* \w3.\w3.\w3.\w3BOO!!!\w8\w1\n\n[half]*AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA!!!!....\e"
			"\s[32]* \w3.\w3.\w3.\w3What was that?..\w8\w1\n\n[half]*I think this was just the wind...\e"
			"\s[32]* \w3.\w3.\w3.\w3We're going into the Manor of %(ghostname)...\e"
			"\s[32]* \w3.\w3.\w3.\w3If you say '%(ghostname)' three times while you're looking into a mirror, \w2you'll be HAUNTED!!..\e"
			"\s[32]* \w3.\w3.\w3.\w3%(ghostname) is just a myth...\w8\w1\n\n[half]*It can't be!..\e"
			"\s[32]* \w3.\w3.\w3.\w3I swear, \w2there's a %(ghostname) under my bed...\e"
			"\s[32]* \w3.\w3.\w3.\w3I'm not scared! \w8\w2You are scared!\e"
			"\s[32]* \w3.\w3.\w3.\w3The name... \w8\w1Written in blood... \w8\w1%(ghostname)...\e"
			"\s[32]* \w3.\w3.\w3.\w3It's in my basement!!\w8\w1\n\n[half]*What is?!?\w8\w1\n\n[half]*%(ghostname)!!!\e"
			"\s[32]* \w3.\w3.\w3.\w3They say %(ghostname) still haunts the halls of this school...\e"
			"\s[32]* \w3.\w3.\w3.\w3They say %(ghostname) lives in these woods...\e"
			"\s[32]* \w3.\w3.\w3.\w3Run! \w8\w2FASTER! \w8\w2R\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2U\w2N\w2!\w2!\w2!\w2!\e"
			"\s[32]* \w3.\w3.\w3.\w3Is it gone?\w2.\w2.\w2. \w8\w2Oh no\w2.\w2.\w2. \w8\w2N\w2O\w2O\w2O\w2O\w2O\w2O\w2O\w2!\w2!\w2!\e"
			"\s[32]* \w3.\w3.\w3.\w3It's an angel! \w8\w2An angel of death!!..\e"
			"\s[32]* \w3.\w3.\w3.\w3you can never escape...\e"
			"\s[32]* \w3.\w3.\w3.\w3Ha\w2.\w2.\w2. \w8\w1ha\w2ha\w2.\w2.\w2. \w8\w1ha\w2ha\w2hA\w2H\w2A\w2H\w2A\w2H\w2A\w2H\w2A\w2H\w2H\w2A\w2H\w2A\w2A\w2!\w2!\w2!\w2!\e"
			"\s[32]* \w3.\w3.\w3.\w3Is that... \w8\w3BLOOD?\w8\w1\n\n[half]*Hoooly heck!!\e"
			"\s[32]* \w3.\w3.\w3.\w3I heard she was kidnapped by %(ghostname)!!\w8\w1\n\n[half]*No way!..\e"
			"\s[32]* \w3.\w3.\w3.\w3Nooo, \w2not my spleen! \w8\w2A\w2u\w2G\w2h\w2H\w2h\w2h\w2!\w2.\w2.\e"
			"\s[32]* \w3.\w3.\w3.\w3...they're eating her! \w8\w2And\nthey're going to eat me! \w8\w2N\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1O\w1-\e"
			"\s[32]* \w3.\w3.\w3.\w3This house was so cheap because %(ghostname) lives in the walls...\e"
			"\s[32]* \w3.\w3.\w3.\w3I saw... \w8\w1%(ghostname)... \w8\w1in a MIRROR!..\e"
			"\s[32]* \w3.\w3.\w3.\w3where's your sister?\w8\w1\n\n[half]*I thought she was with you!\w8\w1\n\n[half]*Oh no...\e"
			"\s[32]* \w3.\w3.\w3.\w3there, \w2in the basement!\e"
			"\s[32]* \w3.\w3.\w3.\w3the hand... \w8\w2of... \w8\w2D\w2E\w2A\w2T\w2H\w2!\w2!\w2!\e"
			"\s[32]* \w3.\w3.\w3.\w3I'm so scared...\w8\w1\n\n[half]*Just a little bit further! \w8\w2We'll make it!\w8\w1\n\n[half]*Leave... \w8\w2me...\w8\w1\n\n[half]*Noooo!..\e"
			"\s[32]* \w3.\w3.\w3.\w3This was done by something... \w8\w2inhuman. \w8\w2AND inmonster...\e"
			"\s[32]* \w3.\w3.\w3.\w3Oh, \w2there's a knock on the door!\w8\w1\n\n[half]*No, \w2don't open it!\w8\w1\n\n[half]*They're ringing the bell!\w8\w1\n\n[half]*NO!\e"
			"\s[32]*(It's footage of a dead body. \w8\w2It's censored by a big white square.)\e"
			"\s[32]*(It's a very scary monster. \w8\w2Played by a very good monster actor.)\e"
			"\s[32]*(It's a very spooky ghost. \w8\w2Not played by an actual ghost.)\e"
			"\s[32]*(A lot of loud screaming.)\e"
			"\s[32]*(It's footage of a very sad-looking tree.)\e"
			"\s[32]*(A lot of blood. \w8\w2It's very obviously ketchup.)\e"
			"\s[32]*(It's footage of an abandoned house.)\e"
			"\s[32]* \w3.\w3.\w3.\w3You knew about this?!\w8\w1\n\n[half]*I'm sorry... \w8\w2It has to be this way.\w8\w1\n\n[half]*Is that... %(ghostname)?!\e"
			"\s[32]* \w3.\w3.\w3.\w3Yes. \w8\w2I was the %(ghostname) ALL ALONG!!!!\e"
		}
		else
		{
			tvwatch = 0
		}
	}
	else
	{
		"\0\s[11]*You can WATCH one of our [3] available channels! \w8\w2\s[13]\i[1]Just tell me what you wish to see!\e"
		"\0\s[0]*Mike!! \w8\w2The lights!! \w8\w2The sounds!! \w8\w2The whatzits!! \w8\w2The doo-zits!! \w8\w2Check them all!!!\e"
		"\0\s[11]\i[1]*You got your snacks and drinks?\e"
		"\0\s[0]\i[1]*Folks, \w2\s[17]remember to sip your water and water your eyes while you're watching!\e"
		"\0\s[23]*(He's grooving.)\e"
		"\0\s[0]\i[1]*Remember to pay attention, \w2folks! \w8\w2\s[13]Keep your eyes on me!\e"
		"\0\s[15]\i[1]*(He's happy  to be watched.)\e"
		"\0\s[11]*%(username), \w2your house is so \w2FUNKY! \w8\w2Take a look at \w3\s[3]THAT!!!\e"
		"\0\s[14]*(He's reading his cards.)\e"
		"\0\s[23]*(He's planning his next show.)\e"
		"\0\s[23]*(He's on his break.)\e"
		"\0\s[13]\i[1]*(He's looking around.)\e"
		"\0\s[12]*Isn't TV so much fun??\e"
		"\0\s[11]*Do \w2\i[1]YOU like cooking? \w8\w2\s[0]\i[1]Check out our cooking channel! \w8\w2\s[18]Just keep your pan at the ready!\e"
		"\0\s[11]*Are \w2\i[1]YOU hungry?? \w8\w2\s[0]\i[1]Check out our cooking channel! \w8\w2\s[0]\i[1]Yummies and tasties await!\e"
		"\0\s[12]*Do you like angry bri'ish guys yelling at people and plates? \w8\w2\s[0]Check out our cooking channel!!\e"
		"\0\s[17]*HUNGRY for content? \w8\w2\s[23]Check out our cooking channel!!\e"
		"\0\s[17]\i[1]*Are \w2YOU lonely? \w8\w2\s[5]I sure know how it feels! \w8\w2\s[6]Project all your life pains on the people over on our \s[11]drama channel!!!\e"
		"\0\s[12]*Craving LOVE??? \w8\w2\s[7]Then the drama channel is for you!\e"
		"\0\s[7]*Craving LOVE??? \w8\w2\s[20]I know I do! \w8\w2\s[23]Make sure to check out the drama channel!!\e"
		"\0\s[8]\i[1]*Craving \w2DRAMA\w2 and [fictional] CONFLICT?? \w8\w2\s[23]Live \w2NOW, \w2over at the drama channel!\e"
		"\0\s[19]*Want to see some \w2ROMANCE? \w8\w2\s[0]Try our drama channel!\e"
		"\0\s[2]\i[1]*Are you in the mood to be \w2\i[1]SCARRED\w2 and \w2\i[1]SCARED?? \w8\w2\s[11]Tune in to the horror channel!!\e"
		"\0\s[11]\i[1]*Do you like \w2\i[1]SPOOKS?? \w8\w2\s[17]Try our horror channel!!\e"
		"\0\s[2]*Feel like \w2SCREAMING??? \w8\w2\s[1]Then our horror channel is just for you!!!\e"
		"\0\s[18]*Are your parents not home? \w8\w2\s[0]Then the horror channel awaits!!\e"
		"\0\s[8]\i[2]*Is your life a \w2LIVING NIGHTMARE? \w8\w2\s[23]Distract yourself with [fake] nightmares over at our horror channel!!\e"
		"\0\s[0]*I hope you're having fun, \w2folks!!\e"
		"\0\s[4]*FUN-O-METER just went up, \w2folks!!\e"
		"\0\s[0]*Our ratings are through the roof!\e"
		"\0\s[11]*If you're running your TV in the background, \w2remember to blow it a kiss once in a while!\e"
		"\0\s[6]*I feel so excited! \w8\w2\s[11]What about you, \w2%(username)?\e"
		"\0\s[7]*It's so nice to be working again!\e"
		"\0\s[13]*I'm happy that someone still appreciates classic TV!\e"
		"\0\s[11]*I hope you're having a TV-tastic time!!!\e"
		"\0\s[13]\i[1]*I hope you've been enjoying the show, \w2%(username)! \w8\w2\s[15]\i[1]I have!!\e"
		"\0\s[0]*Don't TOUCH \w2THAT \w2DIAL, \w3and don't \w2STOP \w2THOSE \w2SMILES!!\e"
		"\0\s[14]*(He's reading his cards. \w8\w2Somehow.)\e"
		"\0\s[4]*Do you wanna watch more? \w8\w2\s[11]\i[1]Stay tuned!\e"
		if ilytv >= 20
		{
			"\0\s[15]\i[1]*(He feels appreciated.)\e"
			"\0\s[13]*(He wonders if you say 'I love TV' more.)\e"
			"\0\s[13]*(He wonders if you say 'I love TV' just one more time.)\e"
		}
		if ilytv >= 100
		{
			"\0\s[13]*(You said 'I love TV' to him so much his nose is tearing up.)\e"
		}
	}
}


//--Chains
//Here is where you will put your bits of chain dialogue. They are outside of OnAiTalk, which makes them a bit more flexible as I mentioned above.
//Each chain function will go through each part of the chain in order even if it's in one function, until it goes to \e:chain=end which will end the chain, as you could probably guess. So you can put the entire chain dialogue into one place.
//You don't have to make chains if you don't want to! This is just for reference if you ever have a conversation come up where it could be handy.

achain
{{CHAIN
	//this will happen the first time the chain is called
	"\0\s[0]Here is the second part of the chain.\w8\1\s[10]Will there be more?\e"
	
	//they will then go to this dialogue next
	"\0\s[0]This is the third part of chained dialogue.\1\w8\s[10]This will lead to a fourth part.\e"
	
	//this will be the last piece of dialogue in the chain, then they will go back to their normal dialogue
	"\0\s[0]This is the fourth piece of chained dialogue.\1\w8\s[10]This is the end.\e:chain=end"
}}CHAIN

//Pay careful attention to the CHAIN text and double brackets for these. Double brackets, CHAIN, dialogue, double brackets, CHAIN. If you are planning on making chains, I'd recommend just copying and pasting the chain example here and changing the dialogue and name of the function as you need. Less likely for mistakes to happen, you know?
//Note also it ends with \e:chain=end, basically ending the chain.

anotherchain
{{CHAIN
	"\0\s[0]This is the second example of chained dialogue.\1\w8\s[10]This will only have two parts.\e"
	
	"\0\s[0]This is the last piece of the second example of chained dialogue\1\w8\s[10]This will end here.\e:chain=end"
}}CHAIN

//You can make chains as long or as shot as you want! Just make sure to end the chain when you're done.


//Alright, we're done with the random conversations for now, but there are a few other things in aitalk that we'll need to fill out too.


//-OnMinuteChange
//These are time-related bits of dialogue for your ghost to say. If any of them look interesting, uncomment them! I commented them out for simplicity's sake, but it's up to you if you want to use them or not!

//OnMinuteChange
//{
//HourCheck
//--
//if mikireflag == 1
	//{
 	//	MikireTalk
	//}
//}


//Keeps track of if an hour has passed. Just ignore this.
HourCheck
{
if passmin >= 59
	{
		passmin = 0
		passhour = passhour + 1
	}
}


OnMinuteChange //This is what will trigger time-based dialogue for your Ghost.
 {
	HourCheck //checks to see if an hour has passed, ignore it
	--
 	if minute == 0 && timecheck == 1 && notatv == 1 //These ifs will check what time it is. Here, it's seeing if the minute equals zero...
	{
 		if hour == 12 //And the hour equals 12, meaning it's noon. You can change this to some other hour if you want.
		{
			if tvtogether == 1
			{
				"\0\s[25]\i[1]*IT'S NOON! \w8\w1\s[28]\i[2]PRIME \s[28]TV TIME!!!\e"
			}
			else
			{
				"\0\s[0]\i[1]*IT'S NOON! \w8\w1\s[12]PRIME TV TIME!!!\e"
			}
			// //Replace with appropriate dialogue as you want.
		}
 		elseif hour == 0 //If the hour is zero, aka midnight. Again, you can change this to whatever hour you want to check for. This section is flexible! You can also add more elseifs if you want to check for other hours.
		{
			if tvtogether == 1
			{
				"\0\s[25]\i[1]*It's midnight! \w8\w1\s[26]The things are about to get interesting, \w2folks!!\e"
			}
			else
			{
				"\0\s[0]\i[1]*It's midnight! \w8\w1\s[19]The most saucy programing begins now, \w2folks!\e"
			}
			// //Then replace the dialogue with what's appropriate.
		}
		else
		{
			if tvtogether == 1
			{
				"\0\s[25]\i[1]*It's \s[25]%(hour12)'o clock!!\e"
				"*(It's %(hour12)'o clock.)\e"
			}
			else
			{
				"\0\s[0]\i[1]*It's %(hour12)'o clock!! \w8\w1\s[17]Onto the next program!\e"
				"\0\s[0]\i[1]*It's %(hour12)'o clock!! \w8\w1\s[17]Stay tuned in, \w2folks!!\e"
				"\0\s[23]*(It's %(hour12)'o clock.)\e"
			}
			// //%(hour12) stores the time. This is a general top of the hour notification. I personally like this because it helps me keep track of time when I've been working on something, so when a ghost is all "Hey it's 3 o clock" I can be like woah really when did that happen. If you think this is annoying though, you can just delete this bit of dialogue to leave this else statement blank, or leave "" in here instead.
		}
	}
	// //You can also check for more specific times! For example...
	// elseif hour == 23 && minute == 55 //this checks to see if the hour is 23 and the minutes are at 55... essentially, 11:55 pm.
	// {
		// "\0\s[0]5 minutes to midnight.\e"
		// //Replace with what you like!
	// }
	// elseif hour == 7 && minute == 30 && weekday == 0 //Here, it's checking for a specific time on a specific day. You can add things to your time checks just like this by using the AND operator, && (more about operators at the walkthrough page). This adds up to 7:30 am, Sunday. If you have a favorite TV program on at some time, you could have your ghost comment on when it comes on like this.
	// {			
		// "\0\s[0]It's 7:30 on Sunday.\e"
		// //Replace with what's appropriate.
	// }
	// //If you're not interested in these more specific checks, you can delete those last two elseifs and just leave the one for minute==0, or clear them all out as mentioned above, although leave in the HourCheck and mikireflag bits.
	
	// 
	
	
	//Advanced users who want to enable the off-screen boundary check = Remember mikireflag up in OnAiTalk? This is the flag that checks for that. You'll want to uncomment this here so it can trigger the dialogue.
	
	//Don't touch this part.
	elseif mikireflag == 1
 	{
 		MikireTalk
	}
 }

//--OnSecondChange
//Like the above, this handles what happens every time a second goes by. Most of this you will not have to touch.

OnSecondChange
 {
	if passsec == 59
 		{
	 	passsec = 0
 		passmin = passmin + 1
		}
 	else
 		{
	 	passsec = passsec + 1
		}
		
		
	//Advanced users - these next bits are related to the mikireflag and when a character is off screen. Uncomment these if you want to use this feature. 
		
	if mikireflag == 0 && reference1 == 1 //Dialogue for putting them off the screen. You could make this a unique line if you wanted (Like "Hey! What are you doing?"), and have MikireTalk below be for if they continue to be offscreen ("Why are you leaving me out here? Help!" for example). I'll add an example bit of dialogue and comment it out, you can uncomment it and use it if you like, or just leave it out and use MikireTalk below.
	{
		MikireTalk
	}
	if mikireflag == 1 && reference1 == 0 //Dialogue for coming back on the screen
	{
		EndMikireTalk
	}
	// mikireflag = reference1 //reference1 is if the ghost is offscreen or not
	
	
	//This dialogue is for when the two characters are overlapping each other. Uncomment this if you want to have dialogue for it!
	
	// if reference2 == 1
	// {
 		// "\0\s[0]Me and Triangle are overlapping.\w8\1\s[19]That's strange since your resolution is %screenwidthx%screenheight, %(username). Shouldn't we have enough space?\4\e"
		
		// //You can see here the use of two envelopes, screenwidth and screenheight, to tell the user their resolution. Oddly, they are not in parentheses. I'm not sure why this is, but they don't display right otherwise. An odd quirk with these two envelopes.
		// //Note also how Triangle moves away at the end of the dialogue. This is due to the \4 tag. That will make any one character move away from the other if they are able. I talked about this tag and its counterpart tag \5 in the SakuraScript page on the walkthrough.
	// }
 }

 //---- MikireTalk ---------------------------------------------------------------------
 //MikireTalk is dialogue for when your main character is off-screen. Like if you dragged Girl over to the side of your monitor until she was partially concealed by it. Advanced users, make sure to uncomment this along with all the other mikire related things in here I've pointed out.
 
MikireTalk
 {
	"\0\s[16]*MIKE, \w2move the camera back on me!!\n\w8\w1I'm out of the shot!!!\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\e"
	"\0\s[23]*(He's such a big shot he's gotten outside the frame.)\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\w8\e"
	// //Note the hotkey combo Triangle mentions. He's telling the truth, that should bring her back on the screen.
 }

 //----EndMikireTalk--------------------------------------------------------------------
 //For after you bring the character back on screen.
 
EndMikireTalk
{
	"\0\s[13]\i[1]*That's better\w\s[0]\i[1] - I don't like being off-stage!\e"	
}


//**** OnSurfaceRestore *****************************************************************
//OnSurfaceRestore is a handy function. What this does is that after a short period of time, the ghost will go back to its default pose, so you won't have a ghost stuck in one expression for too long. It's also helpful if you've made one of the characters disappear using \s[-1] in dialogue and didn't bring them back at the end of it. This will do that for you.
//I'm going to comment out the if/else statements in this because the circumstances in which they'd come up seem pretty unlikely and unnecessary if you're just going to be making a simple ghost. You can just leave this function alone and move on if you want. However, if you do want to use them, just uncomment the lines by deleting the slashes and fill in the info as you need.

//*********************Advanced User Info*******************************
//You can put if statements in OnSurfaceRestore to make exceptions for certain poses, or to make sure that once they're in a certain pose, they stay in that pose. For example, if you turn my Hunter and Smoker ghost into eggplants, there's a check for the their eggplant pose numbers to make sure they STAY eggplants when OnSurfaceRestore is called.
//You can also use OnSurfaceRestore to clean up things that might be left on the screen, primarily from using p[number]\s[] to call up other objects or characters. Just make sure to add a p[number]\s[-1] to OnSurfaceRestore, and that should get rid of it each time the ghost cleans itself up.
//***********************************************************************


idletenna
{
	"s[0]"
	"s[0]"
	"s[0]"
	"s[0]\i[1]"
	"s[7]"
	"s[13]"
	"s[13]"
	"s[13]"
	"s[13]"
	"s[13]\i[1]"
	"s[14]\i[1]"
	"s[15]"
	"s[15]\i[1]"
	"s[23]"
	"s[23]"
	"s[23]"
}

OnSurfaceRestore
 {
 	//if reference0 == 4 || reference0 == 6 //This is checking what poses they are in. This looks to see if Girl is in pose 4 or pose 6, since she is reference0.
 	//{
 	//"\1\s[10]You're in a sad pose.\w8\0\s[0]I'll go back to normal then.\e"
 	//}
 	//elseif reference1 == 14 || reference1 == 16 //This is doing the same but for Triangle, since he's reference1. It looks to see if he's in pose 14 or post 16.
 	//{
 	//"\0\s[0]You look sad.\w8\1\s[10]I'll go back to normal.\e"
 	//}
 	//else
 	//{
	if tvtogether == 1
	{"\0\s[26]\1\s[-1]"}
	elseif tvwatch == 1
	{
		if watchingch1 == 1
		{
			"\0\s[30]\1\s[-1]"
		}
		elseif watchingch2 == 1
		{
			"\0\s[31]\1\s[-1]"
		}
		elseif watchingch3 == 1
		{
			"\0\s[32]\1\s[-1]"
		}
		else
		{
			"\0\s[30]\1\s[-1]"
		}
	}
	elseif notatv == 0
	{"\0\s[29]\1\s[-1]"}
	else
	{"\0\%(idletenna)\1\s[-1]"}
	//}
	//}
 }

 
 //--OnKeyPress
 //This defines the hotkeys your ghost will use. You can change these to whatever you want, but I don't really know why you would. I'd just leave this alone. Advanced Users can add new hotkeys attached to whatever function if you want.
 
OnKeyPress
{
	if reference0 == "t"
	{
	OnAiTalk
	}
	elseif reference0 == "r"
	{
	OnLastTalk
	}
	elseif reference0 == "f1"
	{
	"\![open,readme]"
	}
}